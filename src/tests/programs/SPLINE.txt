SPLINE
Program
Not Archived
accessible
Repeat 3<=N and N<=99
Input "VERTICES=?",N
End
ClrList L1,L2,L3,L4
FnOff 
PlotsOff 
Full
ZDecimal
0->L1(1)
0->L2(1)
Plot1(Scatter,L1,L2,dotplot)
For(I,1,N)
Input 
X->L1(I)
Y->L2(I)
End
DispGraph
"A+BT+CT^^2+DT^^3"->{X1T}
"E+FT+GT^^2+HT^^3"->{Y1T}
"2V^^2-VW-W^^2"->{X2T}
"V^^2+VW-2W^^2"->{Y2T}
"L1(I)-L1(I+1)"->{Y1}
"L1(I+1)-L1(I-1)"->{Y2}
"L1(I-1)-L1(I)"->{Y3}
"L2(I)-L2(I+1)"->{Y4}
"L2(I+1)-L2(I-1)"->{Y5}
"L2(I-1)-L2(I)"->{Y6}
"~.5N(K(I-2)^^2+L(I-1)^^2+MI^^2-2NX(K(I-2)+L(I-1)+MI))"->{Y0}
FnOff 
N-1->N
2->I
{Y1}->K
{Y2}->L
{Y3}->M
{Y0}(0)->L3(1)
For(I,2,N)
{Y1}->K
{Y2}->L
{Y3}->M
{Y0}((I-1)/N)->L3(I)
End
N->I
{Y0}(1)->L3(N+1)
2->I
{Y4}->K
{Y5}->L
{Y6}->M
{Y0}(0)->L4(1)
For(I,2,N)
{Y4}->K
{Y5}->L
{Y6}->M
{Y0}((I-1)/N)->L4(I)
End
N->I
{Y0}(1)->L4(N+1)
""->{Y0}
""->{Y2}:""->{Y3}
""->{Y5}:""->{Y6}
100->S
Repeat S<=0
ClrDraw
S^^-1->S
0->W
For(I,1,N)
W->V
I/N->W
N^^2(~L3(I)VW^^2-L3(I+1)V^^2W-N(V^^3L1(I+1)-W(3V^^2L1(I+1)-3VWL1(I)+W^^2L1(I))))->A
{Y1}->K
~N^^3(L3(I)W{X2T}+V(L3(I+1){Y2T}-6WK))->B
N^^3(L3(I){Y2T}+L3(I+1){X2T}-3K(V+W))->C
N^^2(L3(I)+L3(I+1)+2KN)->D
N^^2(~L4(I)VW^^2-L4(I+1)V^^2W-N(V^^3L2(I+1)-W(3V^^2L2(I+1)-3VWL2(I)+W^^2L2(I))))->E
{Y4}->K
~N^^3(L4(I)W{X2T}+V(L4(I+1){Y2T}-6WK))->F
N^^3(L4(I){Y2T}+L4(I+1){X2T}-3K(V+W))->G
N^^2(L4(I)+L4(I+1)+2KN)->H
For(T,V,W,S)
Pt-On({X1T},{Y1T})
End
End
Pause 
Input "FREQUENCY=?",S
End
Text(0,0,"(C) 1994 MIKAEL BONNIER")
Text(57,0,"LUND, SWEDEN")
""->{X1T}:""->{Y1T}
""->{X2T}:""->{Y2T}
""->{Y1}:""->{Y4}